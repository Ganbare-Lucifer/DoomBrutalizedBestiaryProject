actor DeathDealer
{
  obituary "%o destroyed by the Death Dealer."
  health 1000
  radius 30
  height 80
  meleeDamage 3 
  mass 1000
  speed 7
  Species Angel
  painchance 50
  SEESOUND "angeld/sight"
  painsound "angeld/pain"
  deathsound "angeld/death"
  activesound "angeld/active"
  MONSTER
  +FLOORCLIP
  Tag "Death Dealer"
  states
  {
  Spawn:
    DDAN E 10 A_Look
    loop
  See:
    DDAN AABBCCDD 3 A_Chase
    loop
  Missile:
    D1AN A 0 A_FaceTarget
    D1AN A 0 A_Jump (90,1)
    goto Chaingun
    LMDA A 0
    goto Spikes
  Spikes:
    D1AN A 0 A_UnSetShootable
    D1AN A 0 A_PlaySound("angeld/tel")
    D1AN B 0 A_VileTarget ("BlueBoom0")
    D1AN ABC 3 A_FaceTarget
    D1AN DEFG 4
    D1AN G 0 A_VileTarget ("BlueBoom1")
    D1AN HIJ 3
    D1AN J 0 A_VileTarget ("BlueBoom0")
    D1AN K 25
    D1AN K 0 A_PlaySound("loper/tele2")
    D1AN LMN 3
    D1AN N 0 A_VileTarget ("BlueBoom0")
    D1AN OPQRSBA 4
    D1AN A 0 A_SetShootable
    goto See
  Chaingun:
    DDAN E 10 A_FaceTarget
    DDAN F 5 bright A_CustomMissile ("BulletPierce", 62, 0, 0, 0)
    DDAN G 5 bright A_CustomMissile ("BulletPierce", 62, 0, 0, 0)
    DDAN G 1 A_CPosRefire
    goto Chaingun+1
  Pain:
    DDAN I 2
    DDAN I 2 A_Pain
    D1AN A 0 A_Jump (45, "Spikes")
    Goto See
  Death:
    DDAN J 4 A_Scream
    DDAN KLMNOPQRST 6 A_NoBlocking
    DDAN U -1
    Stop
  Death.Ice:
  Death.Freeze:
  Death.Frost:
	TNT1 A 0 A_ChangeFlag("SHOOTABLE", 0)
	TNT1 A 0 A_ChangeFlag("SOLID", 0)
	TNT1 A 0 A_SpawnItem("FrozenDeathDealer")
	Stop
  Raise:
    DDAN UTSRQPONMLKJ 5
    Goto See
  }
}

actor BlueBoom0
{
  Game Doom
  Alpha 1
  +NOBLOCKMAP
  +NOGRAVITY
  States
  {
  Spawn:
    BMBL ABCDE 4 bright
    BMBL F 7 bright
    BMBL F 0 A_Jump (32, 1)
    goto Missle1
    BMBL F 0 bright A_CustomMissile ("BlueBoomExp1", 0, 0, 0, 2, 0)
    Stop
  Missle1:
    BMBL F 0 bright A_CustomMissile ("BlueBoomExp2", 0, 0, 0, 2, 0)
    Stop
  }
}

actor BlueBoom1
{
  Game Doom
  Alpha 1
  +NOBLOCKMAP
  +NOGRAVITY
  States
  {
  Spawn:
    BMBL GHIJK 4 bright
    BMBL L 7 bright
    BMBL L 0 A_Jump (128, 1)
    goto Missle1
    BMBL L 0 bright A_CustomMissile ("BlueBoomExp1", 0, 0, 0, 2, 0)
    Stop
  Missle1:
    BMBL L 0 bright A_CustomMissile ("BlueBoomExp2", 0, 0, 0, 2, 0)
    Stop
  }
}

actor BlueBoomExp1
{
  radius 25
  height 3
  speed 0
  damage 0
  gravity 5
  deathsound "blueboom/explodebig"
  renderstyle Translucent
  Species Trite
  PROJECTILE
  +RIPPER
  +INVULNERABLE
  +BLOODLESSIMPACT
  +FLOAT
  -NOGRAVITY
  states
  {
  Spawn:
    XXXX A 0
    goto Death
  Death:
    BMBL R 0 A_Scream
    BMBL R 0 A_Explode (60, 135)
    BMBL RSTUV 4 bright
    stop
  }
}

actor BlueBoomExp2
{
  radius 25
  height 3
  speed 0
  damage 0
  gravity 5
  deathsound "blueboom/explode"
  renderstyle Translucent
  Species Trite
  PROJECTILE
  +RIPPER
  +INVULNERABLE
  +BLOODLESSIMPACT
  +FLOAT
  -NOGRAVITY
  states
  {
  Spawn:
    XXXX A 0
    goto Death
  Death:
    BMBX A 0 A_Scream
    BMBX A 0 A_Explode (60, 135)
    BMBX ABCDE 4 bright
    stop
  }
}
